buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.5'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.2"
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'codenarc'
apply plugin: 'maven'
apply plugin: 'maven-publish'

//   _____                            _                 _
//  |  __ \                          | |               (_)
//  | |  | | ___ _ __   ___ _ __   __| | ___ _ __   ___ _  ___  ___
//  | |  | |/ _ \ '_ \ / _ \ '_ \ / _` |/ _ \ '_ \ / __| |/ _ \/ __|
//  | |__| |  __/ |_) |  __/ | | | (_| |  __/ | | | (__| |  __/\__ \
//  |_____/ \___| .__/ \___|_| |_|\__,_|\___|_| |_|\___|_|\___||___/
//              | |
//              |_|

repositories {
    jcenter()
    mavenLocal()
}

dependencies {

    // GRADLE
    compile gradleApi()
    compile 'org.codehaus.groovy:groovy-all:2.3.0'

    // TESTS
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3'
    testCompile 'junit:junit:4.12'
}

//   _____             __ _                       _   _
//  / ____|           / _(_)                     | | (_)
// | |     ___  _ __ | |_ _  __ _ _   _ _ __ __ _| |_ _  ___  _ __
// | |    / _ \| '_ \|  _| |/ _` | | | | '__/ _` | __| |/ _ \| '_ \
// | |___| (_) | | | | | | | (_| | |_| | | | (_| | |_| | (_) | | | |
//  \_____\___/|_| |_|_| |_|\__, |\__,_|_|  \__,_|\__|_|\___/|_| |_|
//                           __/ |
//                          |___/

codenarc {
    configFile = new File(projectDir, 'src/codenarc/rules.groovy')
}

codenarcTest.ignoreFailures = true

//   ____  _       _
//  |  _ \(_)     | |
//  | |_) |_ _ __ | |_ _ __ __ _ _   _
//  |  _ <| | '_ \| __| '__/ _` | | | |
//  | |_) | | | | | |_| | | (_| | |_| |
//  |____/|_|_| |_|\__|_|  \__,_|\__, |
//                                __/ |
//                               |___/

group 'net.kaleidos.gradle.plugins'
version '0.0.1'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    jcenter()
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            groupId 'net.kaleidos.gradle.plugins'
            artifactId 'emerger'
            version '0.0.1'

            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publish = true
    publications = ['mavenCustom']
    pkg {
        name = 'emerger'
        repo = "maven"
        version {
            name = '0.0.1'
        }
    }
}

//   _____          _                    _______        _
//  / ____|        | |                  |__   __|      | |
//  | |    _   _ ___| |_ ___  _ __ ___      | | __ _ ___| | _____
//  | |   | | | / __| __/ _ \| '_ ` _ \     | |/ _` / __| |/ / __|
//  | |___| |_| \__ \ || (_) | | | | | |    | | (_| \__ \   <\__ \
//   \_____\__,_|___/\__\___/|_| |_| |_|    |_|\__,_|___/_|\_\___/
//

task('console', dependsOn:'classes', type: JavaExec) {
    group = 'help'
    description 'starts a Groovy console with the classpath of the current project'
    main='groovy.ui.Console'
    project.dependencies.add("compile", 'org.codehaus.groovy:groovy-console:2.4.0')
    classpath = sourceSets.main.runtimeClasspath
}